plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = mod_group_id
version = mod_version

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            lazyToken('minecraft_classpath') {
                configurations.shadow.files.collect { it.absolutePath }.join(File.pathSeparator)
            }

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
        client { property 'forge.enabledGameTestNamespaces', mod_id }
        server { property 'forge.enabledGameTestNamespaces', mod_id }
    }
}

configurations {
    shadow
    implementation.extendsFrom shadow
}

repositories {
    mavenCentral()
    maven { url = "maven.minecraftforge.net" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shadow 'org.yaml:snakeyaml:1.33'
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'org.yaml.snakeyaml', "${project.group}.shadow.snakeyaml"
    archiveClassifier.set('')
    mergeServiceFiles()
}

reobf {
    shadowJar {}
}

afterEvaluate {
    tasks.named('reobfJar') {
        enabled = false
    }

    tasks.named('jar') {
        finalizedBy 'shadowJar'
    }

    tasks.named('shadowJar') {
        dependsOn 'jar'
    }

    tasks.named('build') {
        dependsOn 'reobfShadowJar'
    }
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version: forge_version,
            forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id,
            mod_name: mod_name,
            mod_license: mod_license,
            mod_version: mod_version,
            mod_authors: mod_authors,
            mod_description: mod_description
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
